module r_pointer_empty #(
    parameter ADDR_WIDTH = 4
)(
    input  wire                   rclk,
    input  wire                   rrst_n,
    input  wire                   r_en,
    input  wire [ADDR_WIDTH:0]    wptr_gray_sync,

    output reg  [ADDR_WIDTH:0]    rptr_gray,
    output reg  [ADDR_WIDTH:0]    rptr_bin,
    output reg                    rempty
);

    wire [ADDR_WIDTH:0] rptr_bin_next;
    wire [ADDR_WIDTH:0] rptr_gray_next;

    assign rptr_bin_next  = rptr_bin + (r_en && !rempty);
    assign rptr_gray_next = (rptr_bin_next >> 1) ^ rptr_bin_next;

    always @(posedge rclk or negedge rrst_n) begin
        if (!rrst_n) begin
            rptr_bin  <= 0;
            rptr_gray <= 0;
        end else begin
            rptr_bin  <= rptr_bin_next;
            rptr_gray <= rptr_gray_next;
        end
    end

    always @(posedge rclk or negedge rrst_n) begin
        if (!rrst_n)
            rempty <= 1'b1;
        else
            rempty <= (rptr_gray_next == wptr_gray_sync);
    end

endmodule
