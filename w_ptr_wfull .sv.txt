module w_ptr_wfull #(
    parameter ADDR_WIDTH = 4
)(
    input  wire                   wclk,
    input  wire                   wrst_n,
    input  wire                   w_en,
    input  wire [ADDR_WIDTH:0]    rptr_gray_sync,

    output reg  [ADDR_WIDTH:0]    wptr_gray,
    output reg  [ADDR_WIDTH:0]    wptr_bin,
    output reg                    wfull
);

    wire [ADDR_WIDTH:0] wptr_bin_next;
    wire [ADDR_WIDTH:0] wptr_gray_next;

    assign wptr_bin_next  = wptr_bin + (w_en && !wfull);
    assign wptr_gray_next = (wptr_bin_next >> 1) ^ wptr_bin_next;

    always @(posedge wclk or negedge wrst_n) begin
        if (!wrst_n) begin
            wptr_bin  <= 0;
            wptr_gray <= 0;
        end else begin
            wptr_bin  <= wptr_bin_next;
            wptr_gray <= wptr_gray_next;
        end
    end

    always @(posedge wclk or negedge wrst_n) begin
        if (!wrst_n)
            wfull <= 1'b0;
        else
            wfull <= (wptr_gray_next == 
                     {~rptr_gray_sync[ADDR_WIDTH:ADDR_WIDTH-1],
                       rptr_gray_sync[ADDR_WIDTH-2:0]});
    end

endmodule
